import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
  id "java"
  id "checkstyle"
  id "com.github.johnrengelman.shadow" version "7.1.0"
  id "io.github.slimjar" version "1.3.0"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
  id "net.kyori.blossom" version "1.3.0"
}

group "io.github.shiruka"

java {
  sourceCompatibility = JavaVersion.VERSION_16
  targetCompatibility = JavaVersion.VERSION_16
}

jar {
  manifest {
    attributes "Implementation-Title": "Shiru ka",
      "Implementation-Version": project.version,
      "Main-Class": "io.github.shiruka.shiruka.Bootstrap"
  }
}

compileJava {
  options.encoding = "UTF-8"
}

tasks.withType(JavaCompiler) {
  options.encoding = "UTF-8"
  options.compilerArgs += ["-Duser.language=en", "-Duser.country=US"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  javadoc.options.encoding = "UTF-8"
  javadoc.options.tags = ["todo"]
  javadoc.options.addBooleanOption("html5", true)
  classifier "javadoc"
  from javadoc.destinationDir
}

checkstyle {
  configFile = file("checkstyle.xml")
  checkstyleTest.enabled = false
}

blossom {
  def constants = "src/main/java/io/github/shiruka/shiruka/Constants.java"
  replaceToken("@version@", project.version, constants)
}

repositories {
  mavenCentral()
  maven {
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    url "https://jitpack.io"
  }
  maven {
    url "https://repo.vshnv.tech/releases/"
  }
  mavenLocal()
}

dependencies {
  implementation "io.github.shiruka:api:3.0.13-SNAPSHOT"
  implementation "io.github.shiruka:network:3.0.4"
  implementation "io.github.shiruka:protocol:1.0.1"

  implementation "io.github.slimjar:slimjar:1.2.6"

  slim "org.jline:jline:3.20.0"
  slim "com.lmax:disruptor:3.4.4"
  slim "info.picocli:picocli:4.6.1"
  slim "it.unimi.dsi:fastutil:8.5.6"
  slim "com.google.inject:guice:5.0.1"
  slim "org.jline:jline-reader:3.20.0"
  slim "org.jline:jline-console:3.20.0"
  slim "io.netty:netty-all:4.1.68.Final"
  slim "it.unimi.dsi:fastutil-core:8.5.6"
  slim "org.jetbrains:annotations:22.0.0"
  slim "com.google.guava:guava:31.0.1-jre"
  slim "net.sf.jopt-simple:jopt-simple:5.0.4"
  slim "org.jline:jline-terminal-jansi:3.21.0"
  slim "org.apache.logging.log4j:log4j-core:2.14.1"
  slim "net.minecrell:terminalconsoleappender:1.3.0"
  slim "org.apache.logging.log4j:log4j-iostreams:2.14.1"
  slim "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"
  slim "com.github.Infumia.InfumiaLib:InfumiaCommon:2.4.5"
  slim "com.fasterxml.jackson.core:jackson-databind:2.13.0"
  slim "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.0"

  annotationProcessor "org.jetbrains:annotations:22.0.0"

  compileOnly "org.projectlombok:lombok:1.18.22"
  annotationProcessor "org.projectlombok:lombok:1.18.20"

  testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
}

test {
  useJUnitPlatform()
}

artifacts {
  archives sourcesJar
  archives javadocJar
  archives shadowJar
}

shadowJar {
  transform(Log4j2PluginsCacheFileTransformer)
  archiveBaseName.set("Shiruka")
  archiveClassifier.set(null)
  archiveVersion.convention(null)
  archiveVersion.set(null)
}
